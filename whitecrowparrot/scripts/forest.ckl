// Welcome to Crackle Script.
//
// This is the Corax Engine event script language.
// Don't be scared, it's extremely easy to use. The language rely on indent of
// four spaces. It is composed of scripts which are evaluated by the Zones
// where it is attached. For instance, a zone named "chest_interaction" has is
// defined as "affected by whitecrow", the script will be evaluated when the
// zone contains the center of "whitecrow".
// The syntax is pretty simple, zero indent is the script declaration level.
// First indent level is the condition to execute the followings jobs and the
// second indent level is the job queue.
//
// List of available keywords:
// declaration: (script, concurrent, event)
// conditional and time related adverbs/function: (always, freeze, nolock,
//                                                 wait)
// comparators: (by, from, has, in, is, overlaps)
// functions: (aim, checkpoint, clear, fadein, fadeout, flush, force, freeze,
//             hide, join, layover, move, pin, play, reach, restart, restore,
//             run, show, set, shift, wait, add, remove, offset)
// corax known objects: (animation, flip, gamepad, globals, hitmap, key,
//                       name, player, pressed, scene, sheet, theatre, zone,
//                       camera, target)
// built-in values: (false, true)


script go_to_tente_with_sword
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow.sheet is sword
    player.whitecrow.animation is idle
        player.whitecrow play tidy_up_sword
        force forest.go_to_tente


script go_to_tente
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
        player.whitecrow reach (432, 10) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim RIGHT by return
        nolock fadeout 10
        player.whitecrow play through_door
        plugin.pecker execute reset
        theatre.scene set tente
        wait 3
        nolock fadein 6
        player.whitecrow move (13, 8)
        player.whitecrow play walk_b
        player.whitecrow play walk_a
        player.whitecrow play walk_off
        player.whitecrow play idle


script go_to_honey_warehouse
    gamepad.keys.pressed has UP
    theatre.globals.jos_is_dead is true
        player.whitecrow reach (44, 10) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim RIGHT by return
        nolock fadeout 10
        player.whitecrow play through_door
        theatre.scene set honeywarehouse
        player.whitecrow move (52, 9)
        wait 3
        nolock fadein 6


script go_to_forest
    theatre.globals.pecker_on_tree_must_be_shown is false
        fadeout 6
        wait 1
        theatre.scene set forest
        player.whitecrow move (432, 10)
        theatre.camera move (4200, 134)
        flush player.whitecrow
        nolock fadein 6


script go_to_forest_show_bird
    theatre.globals.pecker_on_tree_must_be_shown is true
        fadeout 6
        wait 1
        theatre.scene set forest
        player.whitecrow move (432, 10)
        theatre.camera move (4200, 134)
        flush player.whitecrow
        nolock fadein 6
        show prop.pecker_on_tree
        theatre.globals.pecker_on_tree_must_be_shown set false


script go_to_exit_mine
    always
        flush player.whitecrow
        theatre.scene set exit
        flush player.whitecrow
        player.whitecrow move (67, -3)
        player.whitecrow aim LEFT by return
        player.whitecrow play walk_a
        player.whitecrow play walk_b


script go_to_credits
    always
        fadeout 60
        theatre.scene set credits
        fadein 20
        wait 60
        fadeout 20
        restore

///////////////////////////////////////////////////////////////////////////////
//// -- ENVIRONMENT -- ////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


script rabbit_trigger
    theatre.globals.rabbit_triggered is false
        theatre.globals.rabbit_triggered set true
        nolock prop.rabbit play goes_out
        nolock prop.door play opening


concurrent robert_die
    prop.robert.animation is idle
        nolock prop.robert play dying


///////////////////////////////////////////////////////////////////////////////
//// -- BANNER -- /////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


concurrent activate_ladder_prop
    theatre.globals.ladder_is_activated is false
    prop.robert.animation is lie_down
        theatre.globals.ladder_is_activated set true
        prop.ladder move (288, 0)


script pick_ladder
    player.whitecrow.animation is idle
    prop.ladder.hitmap.ground_zone overlaps player.whitecrow.hitmap.leggs
    gamepad.keys.pressed has X
        player.whitecrow join (-2, 10) from prop.ladder by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim LEFT by return
        player.whitecrow.sheet set banner
        prop.ladder move (288, -100)
        player.whitecrow offset (0, -12)
        player.whitecrow play catch


script drop_ladder
    gamepad.keys.pressed has X
    player.whitecrow.animation is idle
    player.whitecrow.sheet is banner
        player.whitecrow aim LEFT by turn
        pin player.whitecrow
        flush player.whitecrow
        player.whitecrow play release
        pin player.whitecrow
        flush player.whitecrow
        prop.ladder move (288, 0)
        player.whitecrow.sheet set exploration
        player.whitecrow offset (0, 12)
        player.whitecrow place prop.ladder by (12, -10)


///////////////////////////////////////////////////////////////////////////////
//// -- JUMP IN WATER -- //////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


script prepare_jump
    player.whitecrow.flip is true
    player.whitecrow.animation is idle
    player.whitecrow.sheet is exploration
    theatre.globals.holding_seeds is false
    plugin.pecker get invisible
        player.whitecrow reach (112, 10) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim LEFT by return
        player.whitecrow.sheet set hivesjump
        player.whitecrow offset (13, 1)
        player.whitecrow play prepare_jump


script cancel_jump
    player.whitecrow.animation is ready_to_jump
    player.whitecrow.sheet is hivesjump
    gamepad.keys.pressed has RIGHT
        player.whitecrow play cancel_jump
        player.whitecrow.sheet set exploration
        player.whitecrow offset (-13, -1)
        player.whitecrow aim RIGHT by return


script jump
    theatre.globals.underwater_ladder_visible is true
    player.whitecrow.animation is ready_to_jump
    player.whitecrow.sheet is hivesjump
    gamepad.keys.pressed has A
        player.whitecrow play jump
        fadeout 35
        theatre.scene set underwater
        player.whitecrow.sheet set underwater
        show static.ladder
        player.whitecrow move (27, 6)
        nolock player.whitecrow play fall
        fadein 35
        theatre.timer.asphixia_countdown init underwater.asphyxia by 1000
        start theatre.timer.asphixia_countdown


script jump2
    theatre.globals.underwater_ladder_visible is false
    player.whitecrow.animation is ready_to_jump
    player.whitecrow.sheet is hivesjump
    gamepad.keys.pressed has A
        player.whitecrow play jump
        fadeout 35
        theatre.scene set underwater
        player.whitecrow.sheet set underwater
        player.whitecrow move (27, 6)
        nolock player.whitecrow play fall
        fadein 35
        theatre.timer.asphixia_countdown init underwater.asphyxia by 1000
        start theatre.timer.asphixia_countdown


script throw_banner
    gamepad.keys.pressed has X
    player.whitecrow.sheet is banner
    player.whitecrow.animation is idle
    player.whitecrow.flip is true
        player.whitecrow play through
        player.whitecrow.sheet set exploration
        player.whitecrow offset (0, 12)
        theatre.globals.underwater_ladder_visible set true
        checkpoint


script tree_crunch_whitecrow
    prop.falling_trunk.hitmap.going_to_fall_here overlaps player.whitecrow.hitmap.head
        player.whitecrow.sheet set death
        player.whitecrow underlay prop.falling_trunk
        player.whitecrow play death_by_mace
        wait 50
        fadeout 30
        restore