// Welcome to Crackle Script.
//
// This is the Corax Engine event script language.
// Don't be scared, it's extremely easy to use. The language rely on indent of
// four spaces. It is composed of scripts which are evaluated by the Zones
// where it is attached. For instance, a zone named "chest_interaction" has is
// defined as "affected by whitecrow", the script will be evaluated when the
// zone contains the center of "whitecrow".
// The syntax is pretty simple, zero indent is the script declaration level.
// Firt indent level is the condition to execute the followings jobs and the
// second indent level is the job queue.
//
// List of available keywords:
// declaration: (script)
// conditional and time related adverbs/function: (always, freeze, nolock,
//                                                 wait)
// comparators: (has, in, is)
// functions: (force, move, play, reach, run, set)
// corax known objects: (animation, gamepad, globals, hitbox, key, sheet,
//                       name, player, pressed, scene, theatre, zone, flip)


script go_to_tente_with_sword
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow.sheet is whitecrowparrot_sword.json
    player.whitecrow.animation is idle
        player.whitecrow play tidy_up_sword
        force forest.go_to_tente


script go_to_tente
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow.sheet is whitecrowparrot_exploration.json
    player.whitecrow.animation is idle
        player.whitecrow play through_door
        theatre.scene set tente
        player.whitecrow play walk_a
        player.whitecrow play walk_b
        player.whitecrow play walk_a
        player.whitecrow play walk_off
        player.whitecrow play idle


script go_to_tente_no_sword
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow.sheet is whitecrowparrot_exploration_no_sword.json
    player.whitecrow.animation is idle
        player.whitecrow play through_door
        theatre.scene set tente
        player.whitecrow.sheet set whitecrowparrot_exploration_no_sword.json
        player.whitecrow play walk_a
        player.whitecrow play walk_b
        player.whitecrow play walk_a
        player.whitecrow play walk_off
        player.whitecrow play idle


script go_to_forest
    always
        theatre.scene set forest
        player.whitecrow move (273, 10)
        theatre.camera move (2600, 134)


///////////////////////////////////////////////////////////////////////////////
//// -- ENVIRONMENT -- ////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


script rabbit_trigger
    theatre.globals.rabbit_triggered is false
        theatre.globals.rabbit_triggered set true
        nolock prop.rabbit play goes_out
        nolock prop.door play opening


///////////////////////////////////////////////////////////////////////////////
//// -- COLLECTS SWORD -- /////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


script collects_sword_get_ready
    player.whitecrow.sheet is whitecrowparrot_exploration_no_sword.json
    player.whitecrow.animation is idle
    prop.sword_holder.animation is attached
    player.whitecrow.flip is false
        player.whitecrow.sheet set whitecrowparrot_collects_sword.json
        player.whitecrow play get_ready


script collects_sword_leave_ready_left
    player.whitecrow.sheet is whitecrowparrot_collects_sword_no_sword.json
    player.whitecrow.animation is is_ready
    gamepad.keys.pressed has LEFT
        player.whitecrow.sheet set whitecrowparrot_exploration.json
        player.whitecrow play return
        player.whitecrow play walk_a


script collects_sword_leave_ready_right
    player.whitecrow.sheet is whitecrowparrot_collects_sword_no_sword.json
    player.whitecrow.animation is is_ready
    gamepad.keys.pressed has RIGHT
        player.whitecrow.sheet set whitecrowparrot_exploration.json
        player.whitecrow.sheet play walk_a


script collects_sword
    gamepad.keys.pressed has X
    player.whitecrow.sheet is whitecrowparrot_collects_sword.json
    player.whitecrow.animation is is_ready
        theatre.globals.has_sword set true
        nolock player.whitecrow play collects
        prop.sword_holder play detached


script footsie_for_sword_collect
    prop.sword_holder.animation is attached
    player.whitecrow.flip is false
    player.whitecrow.animation is idle
        player.whitecrow play footsie