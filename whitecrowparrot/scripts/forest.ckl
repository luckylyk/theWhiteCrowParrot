// Welcome to Crackle Script.
//
// This is the Corax Engine event script language.
// Don't be scared, it's extremely easy to use. The language rely on indent of
// four spaces. It is composed of scripts which are evaluated by the Zones
// where it is attached. For instance, a zone named "chest_interaction" has is
// defined as "affected by whitecrow_gamejam", the script will be evaluated when the
// zone contains the center of "whitecrow_gamejam".
// The syntax is pretty simple, zero indent is the script declaration level.
// Firt indent level is the condition to execute the followings jobs and the
// second indent level is the job queue.
//
// List of available keywords:
// declaration: (script)
// conditional and time related adverbs/function: (always, freeze, nolock,
//                                                 wait)
// comparators: (has, in, is)
// functions: (aim, fadein, fadeout, flush, force, freeze, move, pin, play,
//             reach, restart, run, set, shift, wait)
// corax known objects: (animation, gamepad, globals, hitbox, key, sheet,
//                       name, player, pressed, scene, theatre, zone, flip)


script go_to_tente_with_sword
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow_gamejam.sheet is sword
    player.whitecrow_gamejam.animation is idle
        player.whitecrow_gamejam play tidy_up_sword
        force forest.go_to_tente


script go_to_tente
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow_gamejam.sheet is exploration
    player.whitecrow_gamejam.animation is idle
        player.whitecrow_gamejam reach (272, 10) by (return, walk_a, footsie)
        player.whitecrow_gamejam aim RIGHT by return
        nolock fadeout 6
        player.whitecrow_gamejam play through_door
        theatre.scene set tente
        nolock fadein 6
        player.whitecrow_gamejam move (13, 8)
        player.whitecrow_gamejam play walk_b
        player.whitecrow_gamejam play walk_a
        player.whitecrow_gamejam play walk_off
        player.whitecrow_gamejam play idle


script go_to_forest
    always
        fadeout 6
        theatre.scene set forest
        player.whitecrow_gamejam move (272, 10)
        theatre.camera move (2600, 134)
        flush player.whitecrow_gamejam
        nolock fadein 6


script go_to_exit_mine
    always
        flush player.whitecrow_gamejam
        theatre.scene set exit
        flush player.whitecrow_gamejam
        player.whitecrow_gamejam move (67, -3)
        player.whitecrow_gamejam aim LEFT by return
        player.whitecrow_gamejam play walk_a
        player.whitecrow_gamejam play walk_b


script go_to_credits
    always
        fadeout 60
        theatre.scene set credits
        fadein 20
        wait 60
        fadeout 20
        restart

///////////////////////////////////////////////////////////////////////////////
//// -- ENVIRONMENT -- ////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


script rabbit_trigger
    theatre.globals.rabbit_triggered is false
        theatre.globals.rabbit_triggered set true
        nolock prop.rabbit play goes_out
        nolock prop.door play opening
