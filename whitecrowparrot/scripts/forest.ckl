// Welcome to Crackle Script.
//
// This is the Corax Engine event script language.
// Don't be scared, it's extremely easy to use. The language rely on indent of
// four spaces. It is composed of scripts which are evaluated by the Zones
// where it is attached. For instance, a zone named "chest_interaction" has is
// defined as "affected by whitecrow", the script will be evaluated when the
// zone contains the center of "whitecrow".
// The syntax is pretty simple, zero indent is the script declaration level.
// Firt indent level is the condition to execute the followings jobs and the
// second indent level is the job queue.
//
// List of available keywords:
// declaration: (script)
// conditional and time related adverbs/function: (always, freeze,once, nolock,
//                                                 wait)
// comparators: (has, in, is)
// functions: (force, move, play, reach, run, set)
// corax known objects: (animation, gamepad, globals, hitbox, key, movesheet,
//                       name, player, pressed, scene, theatre, zone)


///////////////////////////////////////////////////////////////////////////////
//// -- SCENE TRANSITION -- ///////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


script go_to_tente_with_sword
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow.movesheet is whitecrowparrot_sword.json
    player.whitecrow.animation is idle
        player.whitecrow play tidy_up_sword
        force forest.go_to_tente


script go_to_tente
    gamepad.keys.pressed has UP
    theatre.scene.name is forest
    player.whitecrow.movesheet is whitecrowparrot_exploration.json
    player.whitecrow.animation is idle
        player.whitecrow play through_door
        theatre.scene set tente
        player.whitecrow play walk_a
        player.whitecrow play walk_b
        player.whitecrow play walk_a
        player.whitecrow play walk_off
        player.whitecrow play idle


script go_to_forest
    always
        theatre.scene set forest
        player.whitecrow move (273, 10)
        theatre.camera move (2600, 134)


///////////////////////////////////////////////////////////////////////////////
//// -- CHEST INTERACTIONS -- /////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


script chest_get_ready
    gamepad.keys.pressed has RIGHT
    theatre.globals.chest_moved is false
    player.whitecrow.movesheet is whitecrowparrot_exploration.json
    player.whitecrow.animation is idle
        player.whitecrow set whitecrowparrot_chest.json
        player.whitecrow play get_ready


script chest_give_up
    gamepad.keys.pressed has LEFT
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
        player.whitecrow play give_up


script chest_try_to_push
    gamepad.keys.pressed has RIGHT
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
    theatre.globals.chest_empty is false
        player.whitecrow play try_to_push


script chest_push_and_die
    gamepad.keys.pressed has RIGHT
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
    theatre.globals.chest_empty is true
    theatre.globals.chest_open is true
        nolock player.whitecrow play push_and_die
        wait 6
        nolock props.chest play move_and_close
        nolock props.armor play nudged
        wait 1
        props.flail play fall


script chest_succes_to_push
    gamepad.keys.pressed has RIGHT
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
    theatre.globals.chest_empty is true
    theatre.globals.chest_open is false
    theatre.globals.chest_moved is false
        theatre.globals.chest_moved set true
        nolock player.whitecrow play push_succes
        wait 4
        props.chest play push_succes
        wait 10


script chest_try_to_open
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
    theatre.globals.chest_locked is true
        nolock player.whitecrow play try_to_open
        wait 30
        nolock props.chest play fail_to_open
        props.chest_locker play up_down


script chest_try_to_open_from_idle
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_exploration.json
    player.whitecrow.animation is idle
    theatre.globals.chest_locked is true
        player.whitecrow set whitecrowparrot_chest.json
        player.whitecrow play get_ready
        nolock player.whitecrow play try_to_open
        wait 30
        nolock props.chest play fail_to_open
        props.chest_locker play up_down


script chest_open
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
    theatre.globals.chest_locked is false
    theatre.globals.chest_moved is false
    theatre.globals.chest_open is false
        nolock player.whitecrow play open_succes
        wait 4
        props.chest play succes_to_open
        theatre.globals.chest_open set true


script chest_open_from_idle
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_exploration.json
    player.whitecrow.animation is idle
    theatre.globals.chest_locked is false
    theatre.globals.chest_moved is false
    theatre.globals.chest_open is false
        player.whitecrow set whitecrowparrot_chest.json
        player.whitecrow play get_ready
        nolock player.whitecrow play open_succes
        wait 4
        props.chest play succes_to_open
        theatre.globals.chest_open set true


script chest_unlock_low
    player.whitecrow.movesheet is whitecrowparrot_sword.json
    player.whitecrow.animation is low_hit_shoot
    theatre.globals.chest_locked is true
        wait 1
        nolock props.chest_locker play explode
        freeze 3
        theatre.globals.chest_locked set false


script chest_unlock_high
    player.whitecrow.movesheet is whitecrowparrot_sword.json
    player.whitecrow.animation is high_hit_shoot
    theatre.globals.chest_locked is true
        nolock props.chest_locker play explode
        freeze 3
        theatre.globals.chest_locked set false


script chest_empty_it_from_idle
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_exploration.json
    player.whitecrow.animation is idle
    theatre.globals.chest_locked is false
    theatre.globals.chest_open is true
    theatre.globals.chest_empty is false
        theatre.globals.chest_empty set true
        player.whitecrow set whitecrowparrot_chest.json
        nolock player.whitecrow play empty_it
        wait 24
        props.chest_props play empty_it
        wait 12
        props.chest_bag play extract


script chest_empty_it_from_ready
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
    theatre.globals.chest_locked is false
    theatre.globals.chest_open is true
    theatre.globals.chest_empty is false
        theatre.globals.chest_empty set true
        player.whitecrow play give_up
        wait 1 // skip 1 frame to let post animation event happen
        player.whitecrow set whitecrowparrot_chest.json
        nolock player.whitecrow play empty_it
        wait 24
        props.chest_props play empty_it
        wait 12
        props.chest_bag play extract


script chest_close_from_idle
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_exploration.json
    player.whitecrow.animation is idle
    theatre.globals.chest_open is true
    theatre.globals.chest_empty is true
        theatre.globals.chest_open set false
        player.whitecrow.movesheet set whitecrowparrot_chest.json
        nolock player.whitecrow play close
        wait 15
        props.chest play close


script chest_close_from_ready
    gamepad.keys.pressed has A
    player.whitecrow.movesheet is whitecrowparrot_chest.json
    player.whitecrow.animation is is_ready
    theatre.globals.chest_open is true
    theatre.globals.chest_empty is true
        theatre.globals.chest_open set false
        player.whitecrow play give_up
        wait 1 // skip 1 frame to let post animation event happen
        player.whitecrow.movesheet set whitecrowparrot_chest.json
        nolock player.whitecrow play close
        wait 15
        props.chest play close
