// Welcome to Crackle Script.
//
// This is the Corax Engine event script language.
// Don't be scared, it's extremely easy to use. The language rely on indent of
// four spaces. It is composed of scripts which are evaluated by the Zones
// where it is attached. For instance, a zone named "chest_interaction" has is
// defined as "affected by whitecrow", the script will be evaluated when the
// zone contains the center of "whitecrow".
// The syntax is pretty simple, zero indent is the script declaration level.
// First indent level is the condition to execute the followings jobs and the
// second indent level is the job queue.
//
// List of available keywords:
// declaration: (script, event)
// conditional and time related adverbs/function: (always, freeze, nolock,
//                                                 wait)
// comparators: (by, from, has, in, is, overlaps)
// functions: (aim, checkpoint, clear, fadein, fadeout, flush, force, freeze,
//             hide, join, layover, move, pin, play, reach, restart, restore,
//             run, show, set, shift, wait, add, remove, offset)
// corax known objects: (animation, flip, gamepad, globals, hitmap, key,
//                       name, player, pressed, scene, sheet, theatre, zone,
//                       camera, target)
// built-in values: (false, true)


script pick_seeds
    gamepad.keys.pressed has X
    player.whitecrow.sheet is exploration
    theatre.globals.chest_empty is true
    theatre.globals.holding_seeds is false
        player.whitecrow.sheet set exploration
        player.whitecrow reach (29, 8) by (return, walk_a, footsie)
        player.whitecrow aim RIGHT by return
        player.whitecrow.sheet set interactions_2
        player.whitecrow layover prop.chest_bag
        nolock player.whitecrow play pick_seeds
        wait 10
        nolock prop.pecker play fly_out
        wait 10
        prop.pecker layover prop.hatch2
        wait 18
        hide prop.pecker
        wait 22
        player.whitecrow layover prop.hatch2
        player.whitecrow.sheet set exploration
        theatre.globals.holding_seeds set true


script throw_seeds_no_bird
    gamepad.keys.pressed has X
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
    theatre.globals.holding_seeds is true
        player.whitecrow.sheet set interactions_2
        vfx.seeds_fx throw (20, -95) from player.whitecrow
        player.whitecrow play throw_seeds
        player.whitecrow.sheet set exploration
        theatre.globals.holding_seeds set false
