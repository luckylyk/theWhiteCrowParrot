// Welcome to Crackle Script.
//
// This is the Corax Engine event script language.
// Don't be scared, it's extremely easy to use. The language rely on indent of
// four spaces. It is composed of scripts which are evaluated by the Zones
// where it is attached. For instance, a zone named "chest_interaction" has is
// defined as "affected by whitecrow", the script will be evaluated when the
// zone contains the center of "whitecrow".
// The syntax is pretty simple, zero indent is the script declaration level.
// Firt indent level is the condition to execute the followings jobs and the
// second indent level is the job queue.
//
// List of available keywords:
// declaration: (script, event)
// conditional and time related adverbs/function: (always, freeze, nolock,
//                                                 wait)
// comparators: (by, from, has, in, is, overlaps)
// functions: (aim, checkpoint, clear, fadein, fadeout, flush, force, freeze,
//             hide, join, layover, move, pin, play, reach, restart, restore,
//             run, show, set, shift, wait, add, remove, offset)
// corax known objects: (animation, flip, gamepad, globals, hitmap, key,
//                       name, player, pressed, scene, sheet, theatre, zone,
//                       camera, target)
// built-in values: (false, true)


script collects_sword_get_ready
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
    prop.sword_holder.animation is attached
    player.whitecrow.flip is false
        player.whitecrow.sheet set collects_sword
        player.whitecrow play get_ready


script collects_sword_leave_ready_left
    player.whitecrow.sheet is collects_sword
    player.whitecrow.animation is is_ready
    gamepad.keys.pressed has LEFT
        player.whitecrow.sheet set exploration
        player.whitecrow play return
        player.whitecrow play walk_a


script collects_sword_leave_ready_right
    player.whitecrow.sheet is collects_sword
    player.whitecrow.animation is is_ready
    gamepad.keys.pressed has RIGHT
        player.whitecrow.sheet set exploration
        player.whitecrow.sheet play walk_a


script collects_sword
    gamepad.keys.pressed has X
    player.whitecrow.sheet is collects_sword
    player.whitecrow.animation is is_ready
        theatre.globals.has_sword set true
        nolock player.whitecrow play collects
        player.whitecrow.layer show sword
        prop.sword_holder play detached
        checkpoint


script footsie_for_sword_collect
    prop.sword_holder.animation is attached
    player.whitecrow.flip is false
    player.whitecrow.animation is idle
        player.whitecrow play footsie


script collects_sword_from_around
    gamepad.keys.pressed has X
    prop.sword_holder.animation is attached
    player.whitecrow.animation is idle
        player.whitecrow reach (508, 10) by (return, walk_a, footsie)
        player.whitecrow aim RIGHT by return
        player.whitecrow.sheet set collects_sword
        player.whitecrow play get_ready
        theatre.globals.has_sword set true
        nolock player.whitecrow play collects
        player.whitecrow.layer show sword
        prop.sword_holder play detached
        checkpoint
