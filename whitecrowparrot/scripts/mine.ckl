// Welcome to Crackle Script.
//
// This is the Corax Engine event script language.
// Don't be scared, it's extremely easy to use. The language rely on indent of
// four spaces. It is composed of scripts which are evaluated by the Zones
// where it is attached. For instance, a zone named "chest_interaction" has is
// defined as "affected by whitecrow", the script will be evaluated when the
// zone contains the center of "whitecrow".
// The syntax is pretty simple, zero indent is the script declaration level.
// First indent level is the condition to execute the followings jobs and the
// second indent level is the job queue.
//
// List of available keywords:
// declaration: (script, concurrent, event)
// conditional and time related adverbs/function: (always, freeze, nolock,
//                                                 wait)
// comparators: (by, from, has, in, is, overlaps)
// functions: (aim, checkpoint, clear, fadein, fadeout, flush, force, freeze,
//             hide, join, layover, move, pin, play, reach, restart, restore,
//             run, show, set, shift, wait, add, remove, offset)
// corax known objects: (animation, flip, gamepad, globals, hitmap, key,
//                       name, player, pressed, scene, sheet, theatre, zone,
//                       camera, target)
// built-in values: (false, true)


script prepare_to_push_the_cart
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
    prop.cart_bg.animation is idle
        player.whitecrow reach (53, -3) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim LEFT by return
        player.whitecrow.sheet set exploration_2
        player.whitecrow play prepare_to_push


script push_cart_with_direction
    gamepad.keys.pressed has LEFT
    player.whitecrow.sheet is exploration_2
    player.whitecrow.animation is ready_to_push
        force mine.push_cart


script push_cart_with_X
    gamepad.keys.pressed has X
    player.whitecrow.sheet is exploration_2
    player.whitecrow.animation is ready_to_push
        force mine.push_cart


script push_cart
    always
        nolock prop.cart_bg play descent
        nolock prop.cart_fg play descent
        hide element.foreground_mask
        wait 5
        nolock player.whitecrow play push_cart
        wait 10
        nolock prop.cart_fg play hidden
        prop.blackknight play hit


script descending_for_snusnu
    gamepad.keys.pressed has LEFT
    player.whitecrow.flip is true
    theatre.globals.wearing_blackhelmet is false
    prop.blackknight.animation in (idle, hidden, entry, hidden_startup)
        player.whitecrow move (29, -3)
        player.whitecrow.sheet set exploration_2
        player.whitecrow play walk_down_start
        player.whitecrow play walk_down_a
        player.whitecrow play walk_down_b
        player.whitecrow play walk_down_a
        player.whitecrow play walk_down_b
        player.whitecrow play walk_down_a
        player.whitecrow move (3, 8)
        player.whitecrow.sheet set death
        nolock prop.blackknight play hidden
        nolock player.whitecrow play stranguled
        wait 15
        fadeout 35
        wait 1
        restore


script descending_for_scavenging
    gamepad.keys.pressed has LEFT
    player.whitecrow.flip is true
    prop.blackknight.animation in (hit, ko)
        player.whitecrow move (29, -3)
        player.whitecrow aim LEFT by return
        player.whitecrow.sheet set exploration_2
        player.whitecrow play walk_down_start
        player.whitecrow play walk_down_a
        player.whitecrow play walk_down_b
        player.whitecrow play walk_down_a
        player.whitecrow play walk_down_b
        player.whitecrow play walk_down_a
        player.whitecrow.sheet set exploration_2
        nolock player.whitecrow play ready_to_get_the_mask
        player.whitecrow move (2, 9)


script climb_normal
    player.whitecrow.sheet is exploration
    player.whitecrow.flip is false
    gamepad.keys.pressed has RIGHT
        player.whitecrow.sheet set exploration_2
        player.whitecrow play walk_up_start
        player.whitecrow play walk_up_a
        player.whitecrow play walk_up_b
        player.whitecrow play walk_up_a
        player.whitecrow play walk_up_b
        player.whitecrow play walk_up_off


script climb_without_getting_the_mask
    gamepad.keys.pressed has RIGHT
    theatre.globals.wearing_blackhelmet is false
    player.whitecrow.sheet is exploration_2
        player.whitecrow.sheet set exploration
        player.whitecrow move (5, 7)
        player.whitecrow aim RIGHT by return
        player.whitecrow.sheet set exploration_2
        player.whitecrow play walk_up_a
        player.whitecrow play walk_up_b
        player.whitecrow play walk_up_a
        player.whitecrow play walk_up_b
        player.whitecrow play walk_up_off


script climb_with_the_mask
    gamepad.keys.pressed has RIGHT
    player.whitecrow.animation is mask_is_get
    theatre.globals.wearing_blackhelmet is true
        player.whitecrow.sheet set exploration
        player.whitecrow move (5, 7)
        player.whitecrow aim RIGHT by return
        player.whitecrow.sheet set exploration_2
        player.whitecrow play walk_up_a
        player.whitecrow play walk_up_b
        player.whitecrow play walk_up_a
        player.whitecrow play walk_up_b
        player.whitecrow play walk_up_off


script getting_the_mask
    gamepad.keys.pressed has X
        nolock prop.blackknight play release_mask
        player.whitecrow play get_the_mask
        player.whitecrow.layer show blackhelmet
        player.whitecrow.layer hide head
        theatre.globals.wearing_blackhelmet set true


script leave_hole_standed_up
    player.whitecrow.animation in (idle, walk_a, walk_b, walk_off, run_a, run_b, run_start, run_off)
        pin player.whitecrow
        flush player.whitecrow
        player.whitecrow play crouch_down_slow
        force mine.leave_hole


script leave_hole
    player.whitecrow.animation in (crouch_down, crouched_over)
        player.whitecrow play crouched_over_walk
        nolock fadeout 30
        player.whitecrow play crouched_over_walk
        player.whitecrow play crouched_over_walk
        theatre.scene set mine
        force mine.cut_scene


script cut_scene
    always
        fadein 15
        flush player.whitecrow
        nolock prop.cut_scene play cut_scene
        nolock prop.lie_down_miner_fg play hidden
        wait 200
        player.whitecrow play crouched_over_walk
        player.whitecrow play crouched_over_walk
        player.whitecrow play crouched_over_walk
        player.whitecrow play crouched_over_walk
        player.whitecrow play stand_up_slow
        nolock prop.blackknight_strangulation play idle
        nolock prop.lie_down_miner_bg play hidden
        nolock prop.lie_down_miner_fg play idle
        wait 30


script denunciation
    theatre.globals.lie_down_miner_is_dead is false
    player.whitecrow.sheet is exploration
    player.whitecrow.animation in (idle, micro_footsie, footsie, walk_a, walk_b run_b, run_a, run_off, walk_off)
        theatre.globals.lie_down_miner_is_dead set true
        prop.lie_down_miner_fg play denunciation
        nolock prop.blackknight_strangulation play walk_out
        player.whitecrow reach (22, 7) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim RIGHT by return
        player.whitecrow play idle
        wait 30
        player.whitecrow.sheet set death
        nolock prop.blackknight_strangulation play hidden
        nolock player.whitecrow play stranguled
        wait 15
        fadeout 35
        wait 1
        restore


script dead_by_snusnu
    prop.blackknight.animation is idle
    player.whitecrow.sheet is exploration
        player.whitecrow reach (11, 9) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim LEFT by return
        wait 1
        pin player.whitecrow
        flush player.whitecrow
        nolock prop.blackknight.animation play hidden
        player.whitecrow.sheet set death
        nolock player.whitecrow play stranguled
        wait 15
        fadeout 35
        wait 1
        restore


script strangulation
    theatre.globals.lie_down_miner_is_dead is false
    player.whitecrow.sheet is exploration
    player.whitecrow.animation in (crouched_over, crouch_down, crouched_over_walk)
        theatre.globals.lie_down_miner_is_dead set true
        prop.blackknight_strangulation play strangulation
        prop.lie_down_miner_fg play strangulation


script miner_face_his_faith
    prop.lie_down_miner_fg.animation is idle
        prop.lie_down_miner_fg play raise_head


script go_to_exit
    always
        player.whitecrow.sheet set exploration_2
        player.whitecrow play leave_climb
        theatre.scene set exit
        force mine.intro_exit


script intro_exit
    always
        player.whitecrow play enter_from_left
        player.whitecrow.sheet set exploration
        nolock prop.blackknight play hidden_startup


script go_to_ending
    always
        // theatre.globals.sinoc_is_alive set false
        flush player.whitecrow
        theatre.scene set ending
        player.whitecrow play walk_a
        player.whitecrow play walk_b
        player.whitecrow play walk_off


script go_to_mine_transit_from_exit
    gamepad.keys.pressed has RIGHT
    player.whitecrow.sheet in (exploration, sword)
        theatre.scene set mine_transit
        player.whitecrow move (3, 1)


script go_to_exit_from_mine_transit
    gamepad.keys.pressed has LEFT
    player.whitecrow.sheet in (exploration, sword)
        theatre.scene set exit
        player.whitecrow move (55, -3)
        player.whitecrow aim LEFT


script go_to_mine_bottom
    gamepad.keys.pressed has DOWN
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
        player.whitecrow reach (16, 1) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim RIGHT by return
        wait 1
        player.whitecrow move (16, 6)
        player.whitecrow.sheet set ladder_transitions
        nolock player.whitecrow play mine_transit_out
        wait 10
        player.whitecrow layover static.fg_overlay
        wait 14
        fadeout 6
        theatre.scene set mine_bottom
        wait 6
        nolock fadein 6
        flush player.whitecrow
        player.whitecrow move (14, 7)
        player.whitecrow play mine_bottom_left_in
        player.whitecrow.sheet set exploration


script go_to_well_border_from_transit
    gamepad.keys.pressed has RIGHT
    player.whitecrow.sheet in (exploration, sword)
        theatre.scene set well_border
        player.whitecrow move (12, 9)


script go_to_transit_from_well_border
    gamepad.keys.pressed has LEFT
    player.whitecrow.sheet in (exploration, sword)
        theatre.scene set mine_transit
        player.whitecrow aim LEFT
        player.whitecrow move (45, 1)


script go_to_transit_from_mine_bottom
    gamepad.keys.pressed has UP
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
        player.whitecrow reach (14, 7) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim LEFT by return
        player.whitecrow play through_door
        theatre.scene set mine_transit
        player.whitecrow layover static.fg_overlay
        player.whitecrow aim RIGHT by return
        player.whitecrow move (16, 6)
        player.whitecrow.sheet set ladder_transitions
        nolock player.whitecrow play mine_transit_in
        wait 20
        player.whitecrow layover static.bridge
        wait 10
        player.whitecrow.sheet set exploration
        player.whitecrow move (17, 1)
        player.whitecrow play idle


script go_to_hill_from_mine_bottom
    gamepad.keys.pressed has UP
    theatre.globals.ladder_cut_scene_played is true
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
        player.whitecrow reach (50, 7) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim RIGHT by return
        player.whitecrow.sheet set ladder_transitions
        player.whitecrow play mine_bottom_right_out
        theatre.scene set hill
        theatre.camera move (1200, 134)
        player.whitecrow move (110, 8)
        player.whitecrow play hill_in
        player.whitecrow move (110, 7)
        player.whitecrow.sheet set exploration
        player.whitecrow play idle


script go_to_hill_from_mine_bottom_with_cut_scene
    gamepad.keys.pressed has UP
    theatre.globals.ladder_cut_scene_played is false
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
        theatre.globals.ladder_cut_scene_played set true
        player.whitecrow reach (50, 7) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim RIGHT by return
        player.whitecrow.sheet set ladder_transitions
        player.whitecrow play mine_bottom_right_out
        theatre.scene set ladder_cut_scene
        player.whitecrow.sheet set ladder_cut_scene
        player.whitecrow aim LEFT
        player.whitecrow play idle
        player.whitecrow.sheet set ladder_transitions
        theatre.scene set hill
        theatre.camera move (1200, 134)
        player.whitecrow move (110, 8)
        player.whitecrow play hill_in
        player.whitecrow move (110, 7)
        player.whitecrow.sheet set exploration
        player.whitecrow play idle


script go_to_mine_bottom_from_hill
    gamepad.keys.pressed has UP
    player.whitecrow.sheet is exploration
    player.whitecrow.animation is idle
        player.whitecrow reach (110, 7) by (return, walk_a, footsie, micro_footsie)
        player.whitecrow aim RIGHT by return
        player.whitecrow play through_door
        theatre.scene set mine_bottom
        player.whitecrow move (50, 7)
        player.whitecrow.sheet set ladder_transitions
        player.whitecrow play mine_bottom_right_in
        player.whitecrow.sheet set exploration
        player.whitecrow aim LEFT by return
